package org.bach.drools.fire.logical;

import org.bach.drools.fire.Fire;
import org.bach.drools.fire.Alarm;
import org.bach.drools.fire.Room;
import org.bach.drools.fire.Sprinkler;

rule RaiseAlarm when
     Fire()
then
    insertLogical( new Alarm( "house1" ) );
    System.out.println( "Raise the Alarm");
end

rule ThereIsAnAlarm when
     Alarm()
then
    System.out.println( "there is an Alarm " );
end

rule ThereIsNoAlarm when
     not Alarm()
then
    System.out.println( "there is no Alarm " );
end

rule TurnSprinklerOn when
    s : Sprinkler( on == false )
    f : Fire( room == s.room )
then
    modify( s ) { setOn( true ) }
    System.out.println( "Turn on the sprinkler for room " + f.getRoom().getName() );
end

rule TurnSprinklerOff when
    s : Sprinkler( on == true )
    not Fire( room == s.room )
then
    modify( s ) { setOn( false ) }
    System.out.println( "Turn off the sprinkler for room " + s.getRoom().getName() );
end

rule OK when
    not Alarm()
    not Sprinkler( on == true )
then
    System.out.println( "Everything is ok" );
end
